import dependencies.Dependencies
import dependencies.Version

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

apply from: Dependencies.dependency

android {
    signingConfigs {
        release {
            def keystoreProps = new Properties()
            def keystorePropsFile = file("${rootDir}/app/keystore/keystore_config.properties")
            if (keystorePropsFile.exists()) {
                keystorePropsFile.withInputStream {keystoreProps.load(it)}
                storeFile file("$keystoreProps.keystoreFile")
                storePassword "$keystoreProps.keystorePassword"
                keyAlias "$keystoreProps.keyAlias"
                keyPassword "$keystoreProps.keyPassword"
            } else {
                storeFile file(System.getenv("KEYSTORE_FILE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    compileSdkVersion Version.compileSdk
    buildToolsVersion Version.buildTools

    defaultConfig {
        applicationId "ru.dzgeorgy.dictionary"
        minSdkVersion Version.minSdk
        targetSdkVersion Version.targetSdk
        versionCode Version.versionCode
        versionName Version.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Version.compose
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0'
}